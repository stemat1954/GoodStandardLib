
page channel in stock@good


[aux] common
{
	int sizeof_request [const];

	begin
	{
		message:begin:fwrite<request>( request:begin, 'b' ):get_size()( sizeof_request );
	}
}



enum type channel_report ( 'good', 'pending', 'busy', 'closed', 'retry', 'end', 'error', 'other' );
{{
	good: all data was transferred as requested; deficit = 0.

	pending:  async request was queued; deficit = size.

	busy: channel is busy, sync request timed out; deficit > 0.  
		
	closed:  deficit = size.

	retry:	The requested amount was not transferred, but retry
	is possible; deficit > 0.
		
	end:  A fixed limit was reached and retry is not possible; 
	deficit > 0.

	error:  deficit > 0.

	other:  unrecognized report from channel
}}
	


[aux] type request
{{
	This is an i/o request data structure.  
			
	:get_group2 should only be called once because text
	output takes ownership of memory allocated by device.

	group2 note is encoded using platform defaults 
}}
{
	general 
	{
		begin	
		{
			ao.action:begin;
			ao.pdata:begin;
			ao.offset:begin;
			ao.size:begin;
			ao.seek:begin;
			ao.id:begin;
			ao.deficit:begin;
			ao.report:begin;
			ao.pnote:begin;
		}


		begin ( ascii action, addr pdata, int offset, int size, int seek [opt], int id [opt] )
		{
			if ( "( action |not=| 'r' ) |and| ( action |not=| 'w' )" ) quit fault ( 'bug', 'invalid i/o action' );
			ascii_to_word( action )( ao.action );
			ao.pdata:begin( pdata );
			ao.offset:begin( offset );
			ao.size:begin( size );
			ao.seek:begin( fix( seek, zero ) );
			ao.id:begin( fix( id, zero ) );
			ao.deficit:begin;
			ao.report:begin;
			ao.pnote:begin;
		}

		begin ( message [upd], expr fex );			-- per image

		fwrite ( message [upd], expr fex );	-- per image

		get_group1 ()( ascii action, addr pdata, int offset, int size, int seek, int id )
		{
			word_to_ascii( ao.action )( action );
			pdata:begin( ao.pdata );
			offset:begin( ao.offset );
			size:begin( ao.size );
			seek:begin( ao.seek );
			id:begin( ao.id );
		}


		get_group2 [upd] ()( int deficit, channel_report cr, text note ) -- invalidates pnote
		{
			deficit:begin( ao.deficit );
			select ( word_to_ascii( ao.report )( =ascii ) )
			{
				case ( 'NUL' )	cr:begin( 'good' );
				case ( 'p' )	cr:begin( 'pending' );
				case ( 'b' )	cr:begin( 'busy' );
				case ( 'c' )	cr:begin( 'closed' );
				case ( 'r' )	cr:begin( 'retry' );
				case ( 'e' )	cr:begin( 'end' );
				case ( 'x' )	cr:begin( 'error' );
				else			cr:begin( 'other' );
			}
			if ( "ao.pnote |not=| no_addr" )
			{
				note:begin:attach( ao.pnote ):trim_zero;  -- note owns the pointer
				ao.pnote:reuse:begin;  -- prevent another attachment
			}
		}
	}



	subroutine
	{
		word_to_ascii ( word8 w )( ascii a )
		{
			a:begin( int:begin( w, 'b:ascii' ) );
		}

		ascii_to_word ( ascii a )( word8 w )
		{
			w:begin( a:as_int, 'b:ascii' );
		}
	}
	

	instance
	{
		word8	action;		-- ascii r, w
		addr	pdata;		-- data addr
		int		offset;		-- placement, bytes
		int		size;		-- transfer size, bytes 
		int		id;			-- for async 
		int		seek;		-- external origin
		int		deficit;	-- size	deficit, bytes
		word8	report;		-- ascii NUL, p, b, t, c, x
		addr	pnote;		-- zero terminated text
	}


	image 'b' [primitive,aligned] 
	{
		action	( 'b' );
		pdata	( 'b' );
		offset	( 'b' );
		size	( 'b' );
		id		( 'b' );
		seek	( 'b' );
		deficit	( 'b' );
		report	( 'b' );
		pnote	( 'b' );
	}
}


[aux] subroutine 
{
	format_request ( request r )( addr pr )  -- orphan pr
	{
		message:begin:fwrite<request>( r, 'b' ):detach()( pr ); 
	}

	restore_request ( addr pr )( request r )  -- final pr 
	{
		message:begin:attach( pr, sizeof_request ):fread<request>( 'b' )( r );
	}

	attach ( message m [var], addr pdata, int offset, int size, int deficit, ascii action ) 
	{
		int msize:begin( "offset + size" );
		m:attach( pdata, msize );
		int cp:begin( msize ):sub( deficit );
		if ( "action |=| 'w'" ) m:set_rp( cp ) else m:set_wp( cp );   
	}
}



subroutine attach_async_message ( addr preq, message m [upd] )( int id, channel_report report, text note ) -- final preq
{
	restore_request( preq )( =request ):get_group1()( ascii action, pdata, int offset, int size, seek, id, addr  ):get_group2()( int deficit, report, note );
	attach( *m, pdata, offset, size, deficit, action );
}

 

type channel [incomplete]  
{{
	A channel is an object for communicating with external
	processes and devices using messages.

	A channel handle is given at :begin time and respons-
	ibility for the handle is assumed by the channel.  The 
	application should not close the handle after calling 
	:begin in any case.

	:close closes the channel normally. An application can
	give it's own closing note. "normal close" is the 
	default. If the channel is already closed, the original 
	closing note is	copied to an output.

	A channel is automatically closed in response to any
	error. In this case, the closing note is copied from
	the i/o request record, if any.  If not provided, the
	closing note is "unspecified channel error."
	
	Once closed, a channel will no longer operate.
		
	Close synchronization is not perfect. In theory, mult-
	iple i/o requests can be initiated and/or completed as
	a channel is closing.  
		
	All i/o requests are guaranteed to complete	if a channel
	is properly closed.  However, it's undefined whether a 
	particular i/o request completes successfully or not
	while the channel is closing.
		
	There can be a race to close a channel.  The winner
	determines the closing note and calls the underlying
	kernel close routine.

	:check checks the channel status.  If it's closed, the
	closing note is copied to an output.  Otherwise if it's
	open, the output will be null.

	The open status flag and closing note are protected by
	an atomic lock.  Once the open status flag is false, 
	the instance is static.

	The maximum wait time for synchronous i/o is max_int
	millisecs, which means "wait forever".  Any etm wait
	time greater than max_int will not properly convert to
	int and the app will be disrupted.
}}
{
	general method
	{
		begin ( handle hch )
		{
			ao.hch:begin( hch );	
			ao.lock:begin;
			ao.open:begin( true );
		}
		
		close [upd]( text note [opt] )( text other )
		{
			fix( note, normal_close ) ~note;
			ao:internal_close( ~note )( other );
		}

		check [upd] ()( text note ) 
		{
			ao.lock:wait;
			if ( "not( ao.open )" ) note:begin( ao.note );  -- why closed
			ao.lock:reset;
		}
	}

	
	base method
	{
		exec_sync_io [upd]( etm wait [opt], ascii action, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:check()( note );
			if ( "null?( note )" ) -- if not closed
			{
				fix( wait, max_int as etm ) ~wait;
				format_request( request:begin( action, pdata, offset, size, seek, null ) )( addr pr );
				kernel\channel_sync_io( ao.hch, pr, ~wait:as_int );
				ao:finish( pr )( deficit, report, note );
			}
			else
			{
				deficit:begin( size );
				report:begin( 'closed' );
			}
		}

		exec_async_io [upd]( int id, handle hq, ascii action, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:check()( note );
			if ( "null?( note )" ) -- if not closed
			{
				format_request( request:begin( action, pdata, offset, size, seek, id ) )( addr pr );
				kernel\channel_async_io( ao.hch, pr, hq );
				ao:finish( pr )( deficit, report, note );
			}
			else
			{
				deficit:begin( size );
				report:begin( 'closed' );
			}
		}
										
		internal_close [upd]( text note )( text other )
		{
			ao.lock:wait;
			if ( ao.open )
			{
				ao.open:not;
				ao.note:begin( note );
				ao.lock:reset;
				kernel\channel_close( ao.hch ); 
			}
			else
			{
				ao.lock:reset;
				other:begin( ao.note );
			}
		}
	}

	misc method
	{
		end
		{
			if ( ao.open ) kernel\channel_close( ao.hch ); 
		}

		finish [upd] ( addr pr )( int deficit, channel_report report, text note )
		{
			restore_request( pr )( =request ):get_group2()( deficit, report, note );
			if ( "report |=| 'error' " )
			{
				fix( note, unspecified_error ) ~note;
				ao:internal_close( ~note )( text first );
				if ( "not_null?( first )" ) note:reuse:begin( first );
			}
		}
	}

	common text 
	{
		normal_close:begin( 'normal close' ) [const];

		unspecified_error:begin( 'unspecified channel error' ) [const];
	}
	
	instance	
	{
		handle		hch;
		toggle		lock;
		bool		open;
		text		note [opt];	-- when closed
	}
}


type sync_channel_reader [incomplete]
{{
	A sync_channel_reader is an adaptable channel reader.
	Messages are transferred synchronously.
	
	:read transfers data from the channel to a given message
	via abstract :~read.

	Data is placed at the current message wp. Request size
	is calculated from message size and wp.
	
	Message wp is updated to reflect the amount of data
	actually transferred.  The application can compare wp
	to message size to calculate a deficit.

	Output report is always well-defined.  If there is no
	deficit, it will be 'good'.  Otherwise, report explains 
	why	there is a deficit.  Output note is channel dependent
	and	may or may not be well defined for any report.
	
	:read can be called in a loop until	message:wp_end? 
	returns true, provided report doesn't imply that another
	read is pointless.
}}
{

	general method
	{
		begin ( etm wait [opt] )
		{
			if ( "not_null?( wait )" ) ao.wait:begin( wait );
		}
	
		read [upd]( message m [upd], int seek [opt], etm wait [opt] )( channel_report report, text note ) 
		{
			m:get_wp()( int offset ):get_size()( =int rsize ):sub( offset );
			if ( "rsize |>| zero" )
			{
				m:detach()( addr pdata );
				ao:~read( fix( wait, ao.wait ), seek, pdata, offset, rsize )( int deficit, report, note ); 
				attach( *m, pdata, offset, rsize, deficit, 'r' );
			}
			else
				report:begin( 'good' ); -- nothing to do
		}
	}

	abstract method
	{
		~read [upd]( etm wait [opt], int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note );
	}
	
	instance
	{
		etm wait [opt];
	}
}


type sync_channel_writer [incomplete]
{{
	A sync_channel_writer is an adaptable channel writer.
	Messages are transferred synchronously.
	
	:write transfers data from a given message to the channel
	via :_write.

	Data is copied from the current message rp. Request size
	is calculated from message size and rp.
	
	Message wp is updated to reflect the amount of data
	actually transferred.  The application can compare wp
	to message size to calculate a deficit.

	Output report is always well-defined.  If there is no
	deficit, it will be 'good'.  Otherwise, report 
	explains why there is a deficit.  Output note is channel
	dependent and may or may not be well defined for any
	report.
	
	:write can be called in a loop until message:rp_end? 
	returns true, provided report doesn't imply that another
	write is pointless.
}}
{
	general method
	{
		begin ( etm wait [opt] )
		{
			if ( "not_null?( wait )" ) ao.wait:begin( wait );
		}
	
		write [upd]( message m [upd], int seek [opt], etm wait [opt] )( channel_report report, text note ) 
		{
			m:get_rp()( int offset ):get_size()( =int rsize ):sub( offset );
			if ( "rsize |>| zero" )
			{
				m:detach()( addr pdata );
				ao:~write( fix( wait, ao.wait ), seek, pdata, offset, rsize )( int deficit, report, note ); 
				attach( *m, pdata, offset, rsize, deficit, 'w' );
			}
			else 
				report:begin( 'good' ); -- nothing to do

		}
	}

	abstract method
	{
		~write [upd]( etm wait [opt], int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note );
	}
	

	instance
	{
		etm wait [opt];
	}
}



type async_channel_reader [incomplete]
{
	general method
	{
		begin
		{
		}
	
		read [upd]( int id, handle hq, message m [upd], int seek [opt] )( channel_report report, text note ) 
		{
			m:get_wp()( int offset ):get_size()( =int rsize ):sub( offset );
			if (" rsize |>| zero ")
			{
				m:detach()( addr pdata );
				ao:~read( id, hq, seek, pdata, offset, rsize )( int deficit, report, note ); 
				if ( "( deficit |not=| rsize ) |or| ( report |not=| 'pending' )" ) attach( *m, pdata, offset, rsize, deficit, 'r' );
			}
			else
				report:begin( 'good' ); -- nothing to do
		}

	}

	abstract method
	{
		~read [upd]( int id, handle hq, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note );
	}
}


type async_channel_writer [incomplete]
{
	general method
	{
		begin
		{
		}
	
		write [upd]( int id, handle hq, message m [upd], int seek [opt] )( channel_report report, text note ) 
		{
			m:get_rp()( int offset ):get_size()( =int rsize ):sub( offset );
			if ( "rsize |>| zero" )
			{
				m:detach()( addr pdata );
				ao:~write( id, hq, seek, pdata, offset, rsize )( int deficit, report, note ); 
				if ( "( deficit |not=| rsize ) |or| ( report |not=| 'pending' )" ) attach( *m, pdata, offset, rsize, deficit, 'w' );
			}
			else
				report:begin( 'good' ); -- nothing to do
		}

	}

	abstract method
	{
		~write [upd]( int id, handle hq, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note );
	}
	
}




type sync_read_only_channel from ( channel, sync_channel_reader )
{
	general
	{
		begin ( handle hch, etm wait [opt] )
		{
			ao:base<channel>:begin( hch );
			ao:base<sync_channel_reader>:begin( wait );
		}
	}

	abstract in sync_channel_reader
	{
		[final] ~read [upd]( etm wait [opt], int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_sync_io( wait, 'r', seek, pdata, offset, size )( deficit, report, note );
		}
	}
}



type sync_write_only_channel from ( channel, sync_channel_writer )
{
	general
	{
		begin ( handle hch, etm wait [opt] )
		{
			ao:base<channel>:begin( hch );
			ao:base<sync_channel_writer>:begin( wait );
		}
	}

	abstract in sync_channel_writer
	{
		[final] ~write [upd]( etm wait [opt], int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_sync_io( wait, 'w', seek, pdata, offset, size )( deficit, report, note );
		}
	}
}


type sync_read_write_channel from ( channel, sync_channel_reader, sync_channel_writer )
{
	general
	{
		begin ( handle hch, etm wait [opt] )
		{
			ao:base<channel>:begin( hch );
			ao:base<sync_channel_reader>:begin( wait );
			ao:base<sync_channel_writer>:begin( wait );
		}
	}

	abstract in sync_channel_reader
	{
		[final] ~read [upd]( etm wait [opt], int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_sync_io( wait, 'r', seek, pdata, offset, size )( deficit, report, note );
		}
	}

	abstract in sync_channel_writer
	{
		[final] ~write [upd]( etm wait [opt], int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_sync_io( wait, 'w', seek, pdata, offset, size )( deficit, report, note );
		}
	}
}



type async_read_only_channel from ( channel, async_channel_reader )
{
	general
	{
		begin ( handle hch )
		{
			ao:base<channel>:begin( hch );
			ao:base<async_channel_reader>:begin;
		}
	}

	abstract in async_channel_reader
	{
		[final] ~read [upd]( int id, handle hq, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_async_io( id, hq, 'r', seek, pdata, offset, size )( deficit, report, note );
		}
		
	}
}


type async_write_only_channel from ( channel, async_channel_writer )
{
	general
	{
		begin ( handle hch )
		{
			ao:base<channel>:begin( hch );
			ao:base<async_channel_writer>:begin;
		}
	}

	abstract in async_channel_writer
	{
		[final] ~write [upd]( int id, handle hq, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_async_io( id, hq, 'w', seek, pdata, offset, size )( deficit, report, note );
		}
	}
}



type async_read_write_channel from ( channel, async_channel_reader, async_channel_writer )
{
	general
	{
		begin ( handle hch )
		{
			ao:base<channel>:begin( hch );
			ao:base<async_channel_reader>:begin;
			ao:base<async_channel_writer>:begin;
		}
	}

	abstract in async_channel_reader
	{
		[final] ~read [upd]( int id, handle hq, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_async_io( id, hq, 'r', seek, pdata, offset, size )( deficit, report, note );
		}
		
	}

	abstract in async_channel_writer
	{
		[final] ~write [upd]( int id, handle hq, int seek [opt], addr pdata, int offset, int size )( int deficit, channel_report report, text note )
		{
			ao:base<channel>:exec_async_io( id, hq, 'w', seek, pdata, offset, size )( deficit, report, note );
		}
	}
}





