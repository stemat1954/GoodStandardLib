
page node in stock@good


{{
	On this page:
	
	node			variable-way base
	node<#>			generic fixed-way base 
	<t>node<#>		generic fixed-way synthetic node 

	node1			specialized one-way base
	<t>node1		specialized one-way synthetic node
}}



type node 
{{
	A node is a multi-link adapatable base type.
	
	Nodes interconnect with other nodes using indexed links.

	A node can have any number of links and link allocation
	is dynamic.  
	
	This node type is used when a nexus interconnects in 
	diverse ways. 

	When node dimension is known and uniform, use a node<#>
	instead.
}}
{
	nom type link_set = array< linked<node> >;

	general method
	{
		begin ( int dim [opt] )
		{
			io.link_set:begin( dim );
		}

		get_link ( int i )( linked<node> nl )
		{
			nl:begin( io.link_set:use( i ) );
		}

		set_link [upd]( int i, linked<node> nl )
		{
			io:make_room( i );
			io.link_set:use( i ):reuse:begin( nl );
		}

		unlink [upd]( int i )
		{
			io.link_set:use( i ):reuse;
		}
	}

	misc method
	{
		make_room [upd]( int i )
		{
			io.link_set:count()( int q );
			i:sub( q )( =int ):add( one )( int need );
			if ( "need |>| zero" ) io.link_set:insert( q, need )
		}
	}

	instance
	{
		link_set;
	}
}


type node<#>
{{
	A node<#> is a generic fixed dimension node.
	
	node<#> linkage is uniform.  That is, a node<#> links
	other node<#>s of the same dimension.

	A node<#> is technically complete, but doesn't have any
	practical application as an actual type. It should be 
	used as a base type for application	types.

	node<'1'> is valid, but specialized node1 is a better
	alternative.
}}
{
	nom type link_set = tuple<<#>>< linked<node<<#>>>;

	general method
	{
		begin  -- all links null
		{
			io.link_set:begin;  -- all links null
		}

		get_link ( int i )( linked<node<<#>>> nl ) 
		{
			io.link_set:use( i ) ~nl;
			if ( "not_null?( ~nl )" ) nl:begin( ~nl );
		}

		set_link [upd](int i, linked<node<<#>>> nl )
		{
			io.link_set:use( i ):reuse:begin( nl );
		}

		unlink [upd]( int i )
		{
			io.link_set:use( i ):reuse;
		}
	}

	instance
	{
		link_set;
	}
}


type <t>node<#> from ( <t>, node<<#>> )
{{
	A <t>node<#> is a generic synthetic node.

	<t> must be a complete type, or it can be an incomplete
	type if it doesn't have any [tbd] abstract methods
	(which forces the synthetic node to be incomplete).

	All methods are inherited from base types.  

	General :begin uses a co-routine to initialize
	application type <t>.

	Default :begin can be used if default <t>:begin
	is available.

	Synthetic nodes of the same dimension and related
	types can interlink.

	Usually, applications will turn some type <t> into
	a linkable type by defining a particular nom type
	for <t>.  For example
	
		nom type thing_node = <thing>node<'2'>.
}}
{
	general method
	{
		begin
		{
			io:as_base< node<<#>> >:begin;
			io:as_base< <t> >:begin;
		}

		begin with make ()( <t> )
		{
			io:as_base< node<<#>> >:begin;
			make()( io:as_base< <t> > );
		}
	}
}



type node1 
{{
	A node1 is a specialized one-way node.
}}
{
	nom type link = linked<node1>;

	general method
	{
		begin  -- null link
		{
		}

		get_link ()( linked<node1> nl )
		{
			if ( "not_null?( io.link )" ) nl:begin( io.link );
		}

		set_link [upd]( linked<node1> nl )
		{
			io.link:reuse:begin( nl );
		}

		unlink [upd]
		{
			io.link:reuse;
		}
	}

	instance
	{
		link;
	}
}




type <t>node1 from ( <t>, node1 )
{{
	specialized synthetic node1
}}
{
	general method
	{
		begin
		{
			io:as_base<node1>:begin;
			io:as_base<<t>>:begin;
		}

		begin with make ()( <t> )
		{
			io:as_base<node1>:begin;
			make()( io:as_base<<t>> );
		}
	}
}




