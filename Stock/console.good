
page console in StockByGood


type console 
{{
	A console is an interactive peripheral that displays 
	ouput strings and accepts input strings from a user. 

	Unicode character encoding is device specific. Call
	:get_encoding to get encoding specs for output strings.

	Note: In general, console encoding is different	from
	platform encoding. However, system consoles always use
	platform encoding.
}}
{
	general method
	{
		begin  -- system console
		{
			kernel\console_identify( no_addr, *handle hcon:begin );
			if ( "hcon |=| no_handle" ) quit with fault:begin( 'platform', 'no system console' );
			io:configure( hcon )( string err );
			if ( "is_not_null?( err )" ) quit with fault:begin( 'platform', 'system console broken' );
		}

		begin ( string name )( string error )  -- named console
		{
			alpha_to_cstr( name )( addr pname ); 
			kernel\console_identify( pname, *handle hcon:begin );
			kernel\heap_free( *pname );

			if ( "hcon |=| no_handle" )
			{
				text_editor:begin( 'unknown console: ' ):write( name ):detach()( error );
				io.ready:begin( false );
			}
			else
				io:configure( hcon )( error );
		}


		get_encoding ()( utf f, endian e )
		{
			f:begin( io.utf );
			e:begin( io.endian );
		}
		
				
		read [upd]( int q )( string input, string error ) -- q chars
		{
			if ( io.ready )
			{
				text_editor ed:begin( io.utf, io.endian );
				io.channel:read( *ed, q )( error );
				if ( "is_null?( error ) ) ed:read()( input ); -- all
			}
			else
				error:begin( not_ready );
		}

		read [upd]( unicode c )( string input, string error ) -- delimiter c
		{
			if ( io.ready )
			{
				text_editor ed:begin( io.utf, io.endian );
				io.channel:read( *ed, c )( error );
				if ( "is_null?( error ) ) ed:read()( input ); -- all
			}
			else
				error:begin( not_ready );
		}

		read_line [upd]()( string input, string error )
		{
			if ( io.ready )
			{
				text_editor ed:begin( io.utf, io.endian );
				io.channel:read( *ed, io.eol )( error );
				if ( "is_null?( error ) ) ed:read()( input ); -- all
			}
			else
				error:begin( not_ready );

		}

		write [upd]( string )( string error )  
		{
			if ( io.ready )
			{
				text_editor ed:begin( given\string );
				io.channel:write( *ed )( error )
			}
			else
				error:begin( not_ready );
		}

		write [upd]( expr )( string error )
		{
			if ( io.ready )
			{
				text_editor ed:begin( given\expr );
				io.channel:write( *ed )( error )
			}
			else
				error:begin( not_ready );
		}

	}


	misc method
	{
		configure [upd]( handle hcon )( string err )
		{
			kernel\console_open_channel( hcon, *handle hch:begin, *addr pnote:begin );
			
			if ( "hch |not=| no_handle" )
			{
				io.channel:begin( hch ); 

				kernel\console_get_protocol( hcon, *int unit:begin, *word16 bom:begin, *int eol:begin );

				io.eol:begin( eol );

				select ( unit )
				{
					value ( '1' ) io.utf:begin ( 'UTF-8' );
					value ( '2' ) io.utf:begin ( 'UTF-16' );
					value ( '4' ) io.utf:begin ( 'UTF-32' );
					else quit with fault:begin( 'bug', 'unexpected code unit value' );
				}

				if ( "bom |=| unicode_bom_FEFF" ) io.endian:begin ( 'big_endian' )
				else if ( "bom |=| unicode_bom_FFFE" ) io.endian:begin ( 'little_endian' )
				else if ( "not( is_zero?( bom ) )" ) quit with fault:begin( 'bug', 'invalid byte order mark' );

				io.ready:begin( true );
			}
			else 
			{
				text_editor ed:begin( 'could not open console channel [' );
				if ( "pnote |not=| no_addr" ) ed:write( cstr_to_alpha( pnote )( =string ) );  -- zero terminated
				ed:write( ']' ):detach()( err );

				io.ready:begin( false );
			}

			kernel\heap_free( *pnote );
		}
	}


	common string not_ready:begin( 'not ready' ) [const];

	instance
	{
		bool					ready; 

		read_write_text_channel channel	[opt];
		utf								[opt];
		endian							[opt];
		unicode					eol		[opt];
	}

}



