page hello in ExamplesByGood


type main_thread from entry
{{
	Most real applications will write a type definition
	like this to define the main thread.  Real apps will
	have an instance and a more elaborate ~run method.  

	The internal application startup routine creates an 
	instance of this type, then calls ~run. 

	Simple applications can choose to write a subroutine
	entry point in lieu of an entry subtype (examples below).
	
	If a subroutine	entry is preferred, the startup routine
	uses an internal entry type definition similar to this
	example	to host the subroutine.
}}
{
	general method
	{
		begin ()()( extra )
		{
			io:as_base<entry>:begin;
		}
	}

	abstract method in entry
	{
		~run [upd]( signal active [upd], signal stop, hp<object> pobj [opt] )
		{
			active:indicate;
			text report:begin;
			isolate
			{
				hello;  -- minimal 
				main( *active, stop );  -- general (sans extra)
				report:write( 'normal exit' );
			}
			trap
			{
				report:write( 'abnormal exit, quit location = ' ):write( ~disruption:use_object( zero ):use_location );
			}
			sys:use_console:write( *report )();
		}
	}
}


subroutine main ( signal active [upd], signal stop )()( extra )
{{
	This is an example of a general subroutine entry point. 
	
	"n - Hello World!" is displayed on the system console 
	every second (where	n is the tick count) until the run-
	time platform indicates stop.

	Note: extras are ignored and disruptions aren't trapped.
	A more general example would recognize extras and trap
	disruptions.
}}
{
	-- event identitiers
	int stop_id:begin( zero );
	int timer_id:begin( one );

	-- timer and tick counter
	timer rhythm:begin:set_repeat( '1 sec' as etm );
	int tick:begin;

	-- event monitor
	monitor events:begin:add( stop, stop_id ):add( rhythm, timer_id );  -- stop has highest priority

	active:indicate;  -- ready

	rhythm:start; 
	loop
	{
		events:wait()( int id );
		
		if ( "id |=| stop_id" ) return;

		-- timer tick

		text greeting:begin:fwrite( tick:inc ):write( ' - Hello World!' );
		sys:use_console:write( *greeting )();
	}
}


subroutine hello 
{{
	This is an example of a minimal subroutine entry point.
	It displays 'Hello World!" on the system console and returns.
}}
{
	text greeting:begin( 'Hello World!' );
--	sys:use_console:write( *'Hello World!' )();
	sys:use_console:write( *greeting )();
}
